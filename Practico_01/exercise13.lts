set TYPE_OBJ = {a, b} //Tipos de objetos
const MAX_OBJ = 2 //Cantidad de maxima de objetos 

//Cinta Transportadora
ONEBUFF=(put[x:TYPE_OBJ] -> get[x] -> ONEBUFF).
||CONVEYOR_BELT(N=MAX_OBJ)=([1..N]:ONEBUFF)/{put/[1].put,
                                             [i:2..N].put/[i-1].get,
                                             get/[N].get}@{put,get}.

//Brazo Mecanico
ARM = (pick_up[x:TYPE_OBJ] -> drop[x] -> ARM).

//Lo de arriba es equivalente a lo de abajo
/*
ARM = (pick_up[{a}] -> drop[{a}] -> ARM
      |pick_up[{b}] -> drop[{b}] -> ARM
      ).
*/

//Prensa
PRESS = (input_press -> press -> release_press -> PRESS)/{drop_to_press/input_press}.

//Taladro
DRILL = (input_drill -> drill -> release_drill -> DRILL)/{drop_to_drill/input_drill}.

//Fabrica
||FACTORY = (CONVEYOR_BELT || arm_begin:ARM || PRESS || DRILL || arm_end:ARM)/{//Renombre del Brazo del INICIO
                                                                               get[x:TYPE_OBJ]/arm_begin.pick_up[x],
                                                                               //Acciones sobre el objeto A
                                                                               drop_to_press/arm_begin.drop.a,
                                                                               release_press/arm_end.pick_up.a,
                                                                               //Acciones sobre el objeto B
                                                                               drop_to_drill/arm_begin.drop.b,
                                                                               release_drill/arm_end.pick_up.b,
                                                                               //Renombre del Brazo DEL FINAL
                                                                               finish/arm_end.drop.TYPE_OBJ}.

//{get[x:{a,b}]/arm_press_drill.pick_up[x]} <==> {get.a/arm_press_drill.pick_up.a, get.b/arm_press_drill.pick_up.b}
//{finished/arm_release.drop.TYPE_OBJ} <==> {finished/arm_release.drop.a, finished/arm_release.drop.b}

