set TYPE_OBJ = {a, b} //Tipos de objetos
const MAX_OBJ = 2 //Cantidad de maxima de objetos 

//Cinta Transportadora
ONEBUFF=(put[x:TYPE_OBJ] -> get[x] -> ONEBUFF).
||CONVEYOR_BELT(N=MAX_OBJ)=([1..N]:ONEBUFF)/{put/[1].put,
                                             [i:2..N].put/[i-1].get,
                                             get/[N].get}@{put,get}.

//Brazo Mecanico
ARM = (pick_up[x:TYPE_OBJ] -> drop[x] -> ARM).

//Lo de arriba es equivalente a lo de abajo
/*
ARM = (pick_up[{a}] -> drop[{a}] -> ARM
      |pick_up[{b}] -> drop[{b}] -> ARM
      ).
*/

//Prensa
PRESS = (input_press -> press -> release_press -> PRESS).

//Taladro
DRILL = (input_drill -> drill -> release_drill -> DRILL).

//Fabrica
||FACTORY = (CONVEYOR_BELT || arm_press_drill:ARM || PRESS || DRILL || arm_release:ARM)/{//Renombre de las entradas de la Prensa y el Taladro
                                                                                         drop_to_press/input_press,
                                                                                         drop_to_drill/input_drill,
                                                                                         //Renombre del Brazo que agarra los objetos
                                                                                         get[x:TYPE_OBJ]/arm_press_drill.pick_up[x],
                                                                                         //Renombre del Brazo que suelta los objetos a la Prensa y el Taladro
                                                                                         drop_to_press/arm_press_drill.drop.a,
                                                                                         drop_to_drill/arm_press_drill.drop.b,
                                                                                         //Renombre del Brazo que agarra los objetos para su terminacion
                                                                                         release_press/arm_release.pick_up.a,
                                                                                         release_drill/arm_release.pick_up.b,
                                                                                         //Renombre del Brazo que libera los objetos
                                                                                         finish/arm_release.drop.TYPE_OBJ}.

//{get[x:{a,b}]/arm_press_drill.pick_up[x]} <==> {get.a/arm_press_drill.pick_up.a, get.b/arm_press_drill.pick_up.b}
//{finished/arm_release.drop.TYPE_OBJ} <==> {finished/arm_release.drop.a, finished/arm_release.drop.b}

//||FACTORY = (CONVEYOR_BELT || arm_press_drill:ARM || PRESS || DRILL || arm_release:ARM)/{//Renombre de las entradas de la Prensa y el Taladro
//                                                                                         drop_to_press/input_press,
//                                                                                         drop_to_drill/input_drill,
//                                                                                         //Renombre del Brazo que agarra los objetos
//                                                                                         get.a/arm_press_drill.pick_up.a,
//                                                                                         get.b/arm_press_drill.pick_up.b,
//                                                                                         //Renombre del Brazo que suelta los objetos a la Prensa y el Taladro                                                                                      
//                                                                                         drop_to_press/arm_press_drill.drop.a,
//                                                                                         drop_to_drill/arm_press_drill.drop.b,
//                                                                                         //Renombre del Brazo que agarra los objetos para su terminacion                                                                                        
//                                                                                         release_press/arm_release.pick_up.a,
//                                                                                         release_drill/arm_release.pick_up.b,
//                                                                                         //Renombre del Brazo que libera los objetos
//                                                                                         finished/arm_release.drop.a,
//                                                                                         finished/arm_release.drop.b}.
